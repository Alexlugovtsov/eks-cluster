# Create a certificate for the ALB with a wildcard domain name
# based on the cluster name and the zone name
resource "aws_acm_certificate" "cert" {
  domain_name = "*.${var.cluster_name}.${var.hosted_zone}"
  validation_method = "DNS"
  subject_alternative_names = ["${var.cluster_name}.${var.hosted_zone}"]

  lifecycle {
    create_before_destroy = true
  }
}

# Create a Route53 record to prove that we own the domain
resource "aws_route53_record" "validation" {
  for_each = {
    for dvo in aws_acm_certificate.cert.domain_validation_options : dvo.domain_name => {
      name   = dvo.resource_record_name
      record = dvo.resource_record_value
      type   = dvo.resource_record_type
    }
  }

  allow_overwrite = true  # we need it because we are using a wildcard domain name
  name    = each.value.name
  records = [each.value.record] # ACM requires a single record
  ttl     = 60                  # ACM requires a TTL of 60 seconds
  type    = each.value.type
  zone_id = data.aws_route53_zone.public.zone_id
}

# Wait for the validation record to be created
resource "aws_acm_certificate_validation" "default" {
  certificate_arn         = aws_acm_certificate.cert.arn
  validation_record_fqdns = [for record in aws_route53_record.validation : record.fqdn]
}
